apply plugin: "maven"

def basePom = {
    project {
        groupId 'com.github.lafa.jsonpath'
        artifactId 'json-path'
        name project.displayName
        packaging 'jar'
        description project.description
        url 'https://github.com/lafaspot/JsonPath'
       
        

        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }

        scm {
            developerConnection 'scm:git:https://github.com/lafaspot/JsonPath.git'
            connection 'scm:git:https://github.com/lafaspot/JsonPath.git'
            url 'https://github.com/lafaspot/JsonPath.git'
            tag 'jsonpath-.1'
        }

        developers {
            developer {
                id 'kalle.stenflo'
                name 'Kalle Stenflo'
                email 'kalle.stenflo (a) gmail.com'
            }
            developer {
                id 'kraman'
                name 'Kumar Raman'
                email 'kumar.raman@yahoo.com'
            }
            developer {
                id 'lafa'
                name 'Luis Alves'
                email 'luis.alves@lafaspot.com'
            }
            
        }
    }
}

def deployers = []

project.afterEvaluate {
    configure(deployers) {
        pom basePom
    }
}

install {
    deployers << repositories.mavenInstaller
}

uploadArchives {
    deployers << repositories.mavenDeployer {
        if (snapshotVersion) {
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                if (project.hasProperty("sonatypeOssUsername")) {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }
        } else {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                if (project.hasProperty("sonatypeOssUsername")) {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }
        }
    }
}

def poms = deployers*.pom
def optionalDeps = []
def providedDeps = []
def internalDeps = []

ext {
    modifyPom = { Closure modification ->
        poms.each {
            it.whenConfigured(modification)
        }
    }
    optional = { optionalDeps << it; it }
    provided = { providedDeps << it; it }
    internal = { internalDeps << it; it }
}

modifyPom { pom ->
    optionalDeps.each { dep ->
        pom.dependencies.find { it.artifactId == dep.name }.optional = true
    }
    providedDeps.each { dep ->
        pom.dependencies.find { it.artifactId == dep.name }.scope = "provided"
    }
    internalDeps.each { dep ->
        pom.dependencies.removeAll { it.artifactId == dep.name }
    }
    // no need to publish test dependencies
    pom.dependencies.removeAll { it.scope == "test" }
}

deployers*.beforeDeployment { signing.signPom(it) }